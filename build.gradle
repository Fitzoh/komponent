group 'komponent'
version '1.0.0-SNAPSHOT'

buildscript {
	ext {
		kotlinVersion = '1.2.30'
	}
	repositories {
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
		classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
	}
}

apply plugin: 'kotlin2js'
apply plugin: 'com.moowork.gulp'

repositories {
	mavenCentral()
	jcenter()
}

// TODO if, for
// TODO find better typed CSS library
// TODO get polymer elements lazily
// TODO if, while components
// TODO generate h1, etc code
// TODO layout vertical / horizontal

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion"
}

task assembleWeb(type: Sync) {
	configurations.compile.each { File file ->
		from(zipTree(file.absolutePath), {
			includeEmptyDirs = false
			include { fileTreeElement ->
				def path = fileTreeElement.path
				path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
						!path.startsWith("META-INF/"))
			}
		})
	}
	from compileKotlin2Js.destinationDir
	into "${projectDir}/js"

	dependsOn classes
}
assemble.dependsOn assembleWeb

task bowerInstall(type: NodeTask) {
	script = file('node_modules/bower/bin/bower')
	args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
			"--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
			"--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
			'install']
	inputs.files file('bower.json')
	outputs.files file('bower_components')
}

bowerInstall.dependsOn npmInstall
gulp_default.dependsOn bowerInstall
//processResources.dependsOn gulp_default
